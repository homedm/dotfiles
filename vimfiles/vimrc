" Last Change: 09/09/2019 - 00:23:28.
" Maintainer: Watanabe Taichi <weasel.wt(at)outlook.com>

" ### Initialization ### {{{
set all&
augroup MyAutoCmd
        autocmd!
augroup END

" in shell setting file, for example .bashrc or .cshrc
" setenv $VIMRUNTIME ~/vim/vim/runtime
" set runtimepath+=~/.vim

if has('vim_starting')
    set encoding=utf-8
    scriptencoding=utf-8
    set t_vb=
    set visualbell
    set noerrorbells
endif

" Windows上でも Unix 形式の end-of-line を使う
" ファイル名内の '\' をスラッシュに置換する
set viewoptions=unix,slash
set number

" disable indent plugin each file type
filetype plugin indent off
set t_Co=256

" enable syntax highlight
set synmaxcol=200
set cmdheight=2
" faster redraw
set lazyredraw
set ttyfast
" Keyword highlight ex. TODO:, NOTE:, REF:, TEMP:
augroup HilightsForce
    autocmd!
    autocmd WinEnter,BufRead,BufNew,Syntax * :silent! call matchadd('Todo', '\(TODO\|NOTE\|REF\|TEMP\):')
    " autocmd WinEnter,BufRead,BufNew,Syntax * highlight Todo guibg=#000124 guifg=#000015
augroup END

if has('terminal')
        set termguicolors
endif
"}}}

" ### encoding ### {{{
" 文字コードの自動認識
set fileencodings=utf-8,utf-16,cp932,iso-2022-jp,euc-jp,sjis
set fileformats=unix,dos,mac
" ファイルに保存される文字エンコーディング
set fileencoding=utf-8
" 改行文字
set fileformat=unix
set ambiwidth=double
" テキスト挿入中の自動折り返しを日本語に対応させる
" auto comment off
autocmd MyAutoCmd FileType * setlocal formatoptions=tcqmM
" }}}

" ### Indent ###{{{
set autoindent " 新しい行のインデントを継続する
set expandtab "tab to space
set tabstop=4 " 画面上でタブ文字の占める幅
set shiftwidth=4 " 自動インデントでずれる幅
set smartindent
" 折り返しの際にインデントを考慮
if exists('+breakindent')
        set breakindent
endif
" }}}

" ### search ### {{{
" インクリメンタル検索を有効にする
set incsearch
" 大文字小文字を無視
set ignorecase
" 大文字が入力されたら大文字小文字を区別する
set smartcase
" }}}

" マウスとの連携機能を On にする
set mouse=a
set ttymouse=xterm2

" ### Buffer ### {{{
" if miss to guess file type
autocmd MyAutoCmd BufWritePost *
                        \ if &filetype ==# '' && exists('b:ftdetect') |
                        \ unlet! b:ftdetect |
                        \ filetype detect |
                        \ endif

" Enable cursor line at needed
augroup vimrc-auto-cursorline
        autocmd!
        autocmd CursorMoved,CursorMovedI * call s:auto_cursorline('CursorMoved')
        autocmd CursorHold,CursorHoldI * call s:auto_cursorline('CursorHold')
        autocmd WinEnter * call s:auto_cursorline('WinEnter')
        autocmd WinLeave * call s:auto_cursorline('WinLeave')
        let s:cursorline_lock = 0
        function! s:auto_cursorline(event)
                if a:event ==# 'WinEnter'
                        setlocal cursorline
                        let s:cursorline_lock = 2
                elseif a:event ==# 'WinLeave'
                        setlocal nocursorline
                elseif a:event ==# 'CursorMoved'
                        if s:cursorline_lock
                                if 1 < s:cursorline_lock
                                        let s:cursorline_lock = 1
                                else
                                        setlocal nocursorline
                                        let s:cursorline_lock = 0
                                endif
                        endif
                elseif a:event ==# 'CursorHold'
                        setlocal cursorline
                        let s:cursorline_lock = 1
                endif
        endfunction
augroup END

" vimの無名レジスタとOSのクリップボードを連携させる
if has('clipboard')
        set clipboard=unnamed,autoselect
endif

" ファイル内容が変更されると自動読み込みする
set autoread
autocmd MyAutoCmd WinEnter * checktime

if has('persistent_undo')
    if !isdirectory(expand('$HOME/.vim/.undo'))
        call mkdir(expand('$HOME/.vim/.undo'), 0700)
    endif
    set undodir=$HOME/.vim/.undo
    set undofile
endif
set viminfo='50,/50,:25,n$HOME/.vim/viminfo
" don't make back up file
set nobackup
" don't make swap file
set noswapfile

" title
set title

" 不可視文字を不可視化
set nolist
" 自動折り返ししない
set nowrap
set textwidth=0
" 長い行を@にさせない
set display=lastline
" 最低でも上下に表示する行数
set scrolloff=5
" 入力したコマンドを画面下に表示
set showcmd

" disable conceal for TeX
let g:tex_conceal='abdmg'
autocmd MyAutoCmd FileType latex :setlocal foldmethod=marker

autocmd MyAutoCmd FileType help call s:set_japanese_document_format()
function! s:set_japanese_document_format()
        " change important keyword the last of lines ' >' and the top of lines '<'
        if &buftype != 'help'
                highlight ignore ctermfg=red
                syntax match Error /\%>79v.*/
                syntax match Error /、\s/
                syntax match Error /。\s/
                set cc=+1
                let JpCountOverChars = 1
                set formatexpr=jpfmt#formatexpr()
                if has('conceal')
                        setlocal conceallevel=0
                endif
        endif
endfunction

set nohlsearch
if v:version > 800
        set nrformats=alpha,hex,bin
endif
" }}}

" ### completion ### {{{
" 入力補完機能を有効化
set wildmenu wildmode=longest:full
set completeopt=menu,preview

" spelling補完 on <C-x><C-s>
set spell
set spelllang+=cjk " 日本語をスペルチェックの対象 から除外する
" When press Ctrl + L while am typing, the previous spelling mistake is
" corrected.
inoremap <C-l> <c-g>u<Esc>[s1z=`]a<c-g>u

" tag Complete For html and xml file
augroup xml-html-comp
    autocmd!
    autocmd FileType html,xml inoremap <buffer> </ </<C-x><C-o>
augroup END

" dictionary Complete
augroup dict-comp
    autocmd!
    autocmd FileType javascript :setlocal dictionary=$HOME/.vim/dict/javascript.dict
    autocmd FileType html :setlocal dictionary=$HOME/.vim/dict/html.dict
    autocmd FileType css :setlocal dictionary=$HOME/.vim/dict/css.dict
    autocmd FileType latex :setlocal dictionary=$HOME/.vim/dict/tex.dict
    autocmd FileType python :setlocal dictionary=$HOME/.vim/dict/python.dict
    autocmd FileType cpp :setlocal dictionary=$HOME/.vim/dict/cpp.dict
    autocmd FileType c :setlocal dictionary=$HOME/.vim/dict/clang.dict
    autocmd FileType ruby :setlocal dictionary=$HOME/.vim/dict/ruby.dict
    autocmd FileType php :setlocal dictionary=$HOME/.vim/dict/php.dict
augroup END
" }}}

" ### Command ### {{{
" the count of command history
set history=2000
" いろんなコマンドの後にカーソルを先頭に移動させない
set nostartofline
" }}}

" ### key mapping ### {{{
" Vim は以下のキーを同一視する.
" <C-i> == <Tab>
" <C-m> == <Enter>
" <C-[> == <ESC>

" Wait key time
set timeout ttimeoutlen=50
" change Leader key to Space key
let mapleader = "\<Space>"

cnoremap <C-n> <Down>
cnoremap <C-p> <Up>
nnoremap ZQ <Nop>
" when tap :wq, make not save and close if buffer is no difference before
cnoremap wq x
" }}}

" ### plugin ### {{{
" automated install vim-plug
if !filereadable(expand('$HOME/.vim/vim-plug/plug.vim'))
    let s:vim_plug_url='https://github.com/junegunn/vim-plug'
    call system("git clone " . s:vim_plug_url . " " . $HOME . "/.vim/vim-plug/")
endif
source ~/.vim/vim-plug/plug.vim

call plug#begin('~/.vim/plugged')
Plug 'cocopon/vaffle.vim'

Plug 'mattn/sonictemplate-vim'
" Tell My Template Directory
let g:sonictemplate_vim_template_dir = [ '~/.vim/template' ]
let g:sonictemplate_vim_vars = {
            \ '_': {
            \   'author': 'Taichi Watanabe',
            \ },
            \}
" For making Plugin
Plug 'mopp/layoutplugin.vim'

" For Completion and Snippet {{{
Plug 'Shougo/neosnippet.vim'
Plug 'Shougo/neosnippet-snippets'
if v:version > 704
    " Enable snipMate compatibility feature.
    let g:neosnippet#enable_snipmate_compatibility = 1
    " Tell Neosnippet about the My Snippets Directory
    let g:neosnippet#snippets_directory=['~/.vim/snippets/']
    " set key map for snippet
    imap <C-k> <plug>(neosnippet_expand_or_jump)
    smap <C-k> <plug>(neosnippet_expand_or_jump)
    xmap <C-k> <plug>(neosnippet_expand_target)
    smap <expr><TAB> neosnippet#expandable_or_jumpable() ?
                \ "\<Plug>(neosnippet_expand_or_jump)" : "\<TAB>"
    if has('conceal')
        set conceallevel=1 concealcursor=niv
    endif
endif
" }}}
"
" Language Server Protocol Client
Plug 'prabirshrestha/asyncomplete.vim'
Plug 'prabirshrestha/async.vim'
Plug 'prabirshrestha/vim-lsp'
Plug 'prabirshrestha/asyncomplete-lsp.vim'

function! s:check_back_space() abort
    let col = col('.') - 1
    return !col || getline('.')[col - 1]  =~ '\s'
endfunction

inoremap <silent><expr> <TAB>
  \ pumvisible() ? "\<C-n>" :
  \ <SID>check_back_space() ? "\<TAB>" :
  \ asyncomplete#force_refresh()
inoremap <expr><S-TAB> pumvisible() ? "\<C-p>" : "\<C-h>"

" common vim-lsp setting
let g:lsp_diagnostics_enabled = 1 " syntactic check enabled by LSP
let g:lsp_diagnostics_echo_cursor = 1
let g:lsp_auto_enable = 1
let g:lsp_use_event_queue = 1
let g:lsp_signs_enabled = 1         " enable signs
let g:lsp_signs_error = {"text": "✗"}
let g:lsp_signs_warning = {"text": "!!"}
let g:lsp_signs_hint = {"text": "??"}
" Opens preview windows as floating
let g:lsp_preview_float = 1
" Preview remains open and waits for an explicit call
let g:lsp_preview_autoclose = 0
autocmd BufWritePre <buffer> LspDocumentFormatSync
nnoremap <buffer> <C-k> :<C-u>LspNextError<CR>

Plug 'fatih/vim-go', { 'do': ':GoUpdateBinaries' }
let g:go_fmt_command = "goimports"
let g:go_bin_path = $GOPATH.'/bin'
let g:go_highlight_types = 1
let g:go_highlight_fields = 1
let g:go_highlight_functions = 1
let g:go_highlight_methods = 1
let g:go_highlight_structs = 1
let g:go_highlight_function_calls = 1
" leave under functions to LSP
let g:go_def_mode = 'gopls'
let g:go_info_mode = 'gopls'
" LSP に任せる機能を Off にする
let g:go_def_mapping_enabled = 0
let g:go_doc_keywordprg_enabled = 0

if executable('go-langserver')
    augroup LspGo
        autocmd!
        autocmd User lsp_setup call lsp#register_server({
                    \ 'name': 'go-lang',
                    \ 'cmd': {server_info->['gopls']},
                    \ 'rootPatterns': ["go.mod", ".git/"],
                    \ 'whitelist': ['go'],
                    \ })
        autocmd FileType go setlocal omnifunc=lsp#complete
    augroup END
endif

" For JavaScript
Plug 'ryanolsonx/vim-lsp-javascript'
" Typescript Syntax highlight
Plug 'leafgarland/typescript-vim'
if executable('typescript-language-server')
    " npm install -g typescript typescript-language-server
    " use directory with .git as root
    autocmd User lsp_setup call lsp#register_server({
    \ 'name': 'javascript',
    \ 'cmd': {server_info->[&shell, &shellcmdflag, 'typescript-language-server --stdio']},
    \ 'root_uri':{server_info->lsp#utils#path_to_uri(lsp#utils#find_nearest_parent_file_directory(lsp#utils#get_buffer_path(), 'package.json'))},
    \ 'whitelist': ['javascript', 'javascript.jsx'],
    \ })
    autocmd User lsp_setup call lsp#register_server({
        \ 'name': 'typescript',
        \ 'cmd': {server_info->[&shell, &shellcmdflag, 'typescript-language-server --stdio']},
        \ 'root_uri':{server_info->lsp#utils#path_to_uri(lsp#utils#find_nearest_parent_file_directory(lsp#utils#get_buffer_path(), 'tsconfig.json'))},
        \ 'whitelist': ['typescript', 'typescript.tsx'],
        \ })
endif

if executable('css-languageserver')
    " npm install -g vscode-css-languageserver-bin
    augroup LspCSS
        autocmd!
        autocmd User lsp_setup call lsp#register_server({
                    \ 'name': 'CSS, SCSS, LESS',
                    \ 'cmd': {server_info->[&shell, &shellcmdflag, 'css-languageserver --stdio']},
                    \ 'whitelist': ['css', 'less', 'sass'],
                    \ })
        autocmd FileType css,lss,sass setlocal omnifunc=lsp#complete
    augroup END
endif

Plug 'mattn/emmet-vim'

" npm install -g intelephense-server
" npm list -g | head
augroup LspPHP
    autocmd!
    autocmd User lsp_setup call lsp#register_server({
                \ 'name': 'PHP',
                \ 'cmd': {server_info->['node', expand('~/.nvm/versions/node/v11.7.0/lib/node_modules/intelephense-server/lib/server.js'), '--stdio']},
                \ 'whitelist': ['php'],
                \ })
    autocmd FileType php setlocal omnifunc=lsp#complete
augroup END

if executable('solagraph')
    " gem install solagraph
    augroup LspRuby
        autocmd!
        autocmd User lsp_setup call lsp#register_server({
                    \ 'name': 'Ruby',
                    \ 'cmd': {server_info->[&shell, &shellcmdflag, 'solagraph stdio']},
                    \ 'initialization_options': { "diagnostics": "true"},
                    \ 'whitelist': ['ruby'],
                    \ })
        autocmd FileType ruby setlocal omnifunc=lsp#complete
    augroup END
endif
autocmd MyAutoCmd FileType ruby :setlocal isk+=@-@

" pip install python-language-server
Plug 'ryanolsonx/vim-lsp-python'
if executable('pyls')
    au User lsp_setup call lsp#register_server({
        \ 'name': 'Python',
        \ 'cmd': {server_info->['pyls']},
        \ 'whitelist': ['python'],
        \ })
endif

Plug 'derekwyatt/vim-scala'
if executable('metals-vim')
   au User lsp_setup call lsp#register_server({
      \ 'name': 'Scala',
      \ 'cmd': {server_info->['metals-vim']},
      \ 'initialization_options': { 'rootPatterns': 'build.sbt' },
      \ 'whitelist': [ 'scala', 'sbt' ],
      \ })
endif

" npm install -g dockerfile-language-server-nodejs
if executable('docker-langserver')
    au User lsp_setup call lsp#register_server({
        \ 'name': 'docker-langserver',
        \ 'cmd': {server_info->[&shell, &shellcmdflag, 'docker-langserver --stdio']},
        \ 'whitelist': ['dockerfile'],
        \ })
endif

" other language lsp setting
augroup LspEFM
  au!
  autocmd User lsp_setup call lsp#register_server({
      \ 'name': 'efm',
      \ 'cmd': {server_info->['efm-langserver']},
      \ 'whitelist': ['markdown', 'vim'],
      \ })
augroup END

" C Language
Plug 'justmao945/vim-clang'
" Lisp
Plug 'kovisoft/slimv'

" Runner
Plug 'thinca/vim-quickrun' " {{{
nnoremap <expr><silent> <space>c quickrun#is_runnting() ? quickrun#sweep_sessions() "\<C-c>"
" <Leader>q で quickrun で開かれた出力バッファを閉じる
nnoremap <Leader>q :<C-u>bw! \[QuickRun\ Output\]<CR>
let g:quickrun_config = {
            \ '_' : {
            \   'outputter/buffer/split' : ':botright 8sp',
            \   'outputter/buffer/close_on_empty' : 1,
            \   'outputter/buffer/name' : '[QuickRun Output]',
            \ },
            \ 'c/gcc' : {
            \   'cmdopt' : '-stc=c99 -Wall'
            \ },
            \ 'tex' : {
            \   'command' : 'platex',
            \   'exec' : ['%c -synctex=1 -interaction=nonstopmode %s', 'zathura %s:r.pdf']
            \ },
            \ 'lisp' : {
            \   'type': executable('sbcl') ? 'lisp/sbcl':
            \           executable('ccl')  ? 'lisp/ccl':
            \           executable('clisp') ? 'lisp/clisp': '',
            \ },
            \ 'lisp/sbcl': {
            \   'command': 'sbcl',
            \   'cmdopt': '--script'
            \ },
            \ 'lisp/ccl' : {
            \   'command': 'ccl',
            \   'exec': '%c -l %s -e "(ccl:quit)"',
            \ },
            \ 'lisp/clisp': {
            \   'command': 'clisp',
            \ }
            \ }
" }}}

" Color Scheme
Plug 'cocopon/iceberg.vim', {'do': 'cp colors/* ~/.vim/colors/'}
Plug 'altercation/solarized', {'do': 'cp vim-colors-solarized/colors/* ~/.vim/colors/'}

" Status Line
Plug 'itchyny/vim-gitbranch'
Plug 'itchyny/lightline.vim'
" display status line
set laststatus=2
set ruler " add cursor line location in right of status line
let g:lightline = {
            \ 'active': {
            \   'left': [ ['mode', 'gitbranch', 'readonly', 'filename', 'modified'] ]
            \ },
            \ 'colorscheme': 'solarized',
            \ 'component_function': {
            \   'gitbranch': 'gitbranch#name'
            \ },
            \ }
" Syntastic can call a post-check hook, let's update lightline there
" For more information: :help syntastic-loclist-callback
function! SyntasticCheckHook(errors)
    call lightline#update()
endfunction

" For CSV
Plug 'luochen1990/rainbow'
let g:rainbow_active=1 "0 if you want to enable it later via :RainbowToggle
Plug 'mechatroner/rainbow_csv'

" For General Formatter
Plug 'vim-scripts/autodate.vim',
let autodate_format="%d/%m/%Y\ -\ %H:%M:%S"
Plug 'fuenor/JpFormat.vim',
" gqコマンドを実行前に自動整形をオフにする
nnoremap <silent> <expr> gq JpFormat_cmd("gq")
" .txt, .mdで自動整形を有効にする。
autocmd MyAutoCmd FileType markdown call s:set_autoformat()
autocmd MyAutoCmd FileType text call s:set_autoformat()
function! s:set_autoformat()
    let JpCountChars=80
    let JpCountOverChars=1
    set formatexpr=jpfmt#formatexpr()
endfunction
Plug 'homedm/vim-better-whitespace'
Plug 'vim-scripts/Align'
let g:Align_xstrlen = 3 " Setting for Japanese environment

Plug 'vim-jp/vimdoc-ja'
" japanese help
set helplang=en,ja

" For Writing Documentation
Plug 'glidenote/memolist.vim'
let g:memolist_path = expand('$HOME/code/src/github.com/homedm/notebox/memo')
let g:memolist_template_dir_path = expand('$HOME/.vim/template/memolist/')
let g:memolist_memo_suffix = "md"
let g:memolist_memo_date = "%Y-%m-%d %H:%M"
" markdown
Plug 'previm/previm'
" open with chromium
let g:previm_open_cmd = 'chromium'
" For LaTeX
Plug 'lervag/vimtex'
let g:tex_flavor='latex'
let g:vimtex_view_method='zathura'
let g:vimtex_quickfix_mode=0
let g:vimtex_compiler_latexmk = {
            \ 'background': 1,
            \ 'build_dir': '',
            \ 'continuous': 1,
            \ 'options': [
            \     '-pdfdvi',
            \     '-verbose',
            \     '-file-line-error',
            \     '-synctex=1',
            \     '-interaction=nonstopmode',
            \ ],
            \}

Plug 'andymass/vim-matchup'
set showmatch

" Draw image and play music
Plug 'thinca/vim-painter'
Plug 'homedm/vim-play-piano'

call plug#end()
packadd termdebug

" netrw setting
let g:netrw_liststyle = 1
let g:netrw_sizestyle = "H"
let g:netrw_sort_options="i"
let g:netrw_timefmt = "%Y/%m/%d(%a) %H:%M:%S"
let g:netrw_preview = 1
let g:netrw_list_hide = '.git'
let g:netrw_altv = 1
let g:netrw_alto = 1
" }}}

" should write last line {{{
filetype plugin indent on
" syntax on の場合は、現在の色設定を変更する.また、syntax on, syntax enable は
" , runtimepath に含まれている設定をもとにシンタックスを生成しようとするため,
" runtimepath を設定し終えた後に設定するべき.
syntax enable
set background=dark
let g:solarized_termcolors=256
colorscheme solarized
set secure
" }}}
