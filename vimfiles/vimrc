" Last Change: 07/08/2019 - 10:12:41.
" Maintainer: Watanabe Taichi <weasel.wt(at)outlook.com>

" ### Initialization ### {{{
set nocompatible
set all&
augroup MyAutoCmd
        autocmd!
augroup END

" in shell setting file, for example .bashrc or .cshrc
" setenv $VIMRUNTIME ~/vim/vim/runtime
set runtimepath+=~/.vim

" japanese help
set helplang=en,ja

if has('vim_starting')
    set encoding=utf-8
    scriptencoding=utf-8
endif

" windows上でもunix形式のend-of-lineを使う
" ファイル名内の'\'をスラッシュに置換する
set viewoptions=unix,slash

" disable indent plugin each filetype
filetype plugin indent off
set t_Co=256

" enable syntax highlight
syntax on
set synmaxcol=200
set cmdheight=2
" faster redraw
set lazyredraw
set ttyfast
" Keyword highlight ex. TODO:, NOTE:, REF:, TEMP:
augroup HilightsForce
    autocmd!
    autocmd WinEnter,BufRead,BufNew,Syntax * :silent! call matchadd('Todo', '\(TODO\|NOTE\|REF\|TEMP\):')
    autocmd WinEnter,BufRead,BufNew,Syntax * highlight Todo guibg=Red guifg=White
augroup END


" ### terminal ### {{{
if has('terminal')
        set termguicolors
endif
"}}}
"}}}

" ### encoding ### {{{
" 文字コードの自動認識
set fileencodings=utf-8,utf-16,cp932,iso-2022-jp,euc-jp,sjis
set fileformats=unix,dos,mac
" ファイルに保存される文字エンコーディング
set fileencoding=utf-8
" 改行文字
set fileformat=unix
set ambiwidth=double
" テキスト挿入中の自動折り返しを日本語に対応させる
" auto commet off
autocmd MyAutoCmd BufEnter * setlocal formatoptions=tcqmM
" }}}

" ### Indent ###{{{
set autoindent " 新しい行のインデントを継続する
set expandtab "tab to space
set tabstop=4 " 画面上でタブ文字の占める幅
set shiftwidth=4 " 自動インデントでずれる幅
set smartindent
" 折り返しの際にインデントを考慮
if exists('+breakindent')
        set breakindent
endif
" }}}

" ### search ### {{{
" インクリメンタル検索を有効にする
set incsearch
" 大文字小文字を無視
set ignorecase
" 大文字が入力されたら大文字小文字を区別する
set smartcase
" }}}

" マウスとの連携機能を On にする
set mouse=a
set ttymouse=xterm2

" ### Buffer ### {{{
" if miss to guess filetype
autocmd MyAutoCmd BufWritePost *
                        \ if &filetype ==# '' && exists('b:ftdetect') |
                        \ unlet! b:ftdetect |
                        \ filetype detect |
                        \ endif

" Enable cursorline at needed
augroup vimrc-auto-cursorline
        autocmd!
        autocmd CursorMoved,CursorMovedI * call s:auto_cursorline('CursorMoved')
        autocmd CursorHold,CursorHoldI * call s:auto_cursorline('CursorHold')
        autocmd WinEnter * call s:auto_cursorline('WinEnter')
        autocmd WinLeave * call s:auto_cursorline('WinLeave')
        let s:cursorline_lock = 0
        function! s:auto_cursorline(event)
                if a:event ==# 'WinEnter'
                        setlocal cursorline
                        let s:cursorline_lock = 2
                elseif a:event ==# 'WinLeave'
                        setlocal nocursorline
                elseif a:event ==# 'CursorMoved'
                        if s:cursorline_lock
                                if 1 < s:cursorline_lock
                                        let s:cursorline_lock = 1
                                else
                                        setlocal nocursorline
                                        let s:cursorline_lock = 0
                                endif
                        endif
                elseif a:event ==# 'CursorHold'
                        setlocal cursorline
                        let s:cursorline_lock = 1
                endif
        endfunction
augroup END

" vimの無名レジスタとOSのクリップボードを連携させる
if has('clipboard')
        set clipboard=unnamed,autoselect
endif

" ファイル内容が変更されると自動読み込みする
set autoread
autocmd MyAutoCmd WinEnter * checktime

if has('persistent_undo')
    if !isdirectory(expand('$HOME/.vim/.undo'))
        call mkdir(expand('$HOME/.vim/.undo'), 0700)
    endif
    set undodir=$HOME/.vim/.undo
    set undofile
endif
set viminfo='50,/50,:25,n$HOME/.vim/viminfo
" don't make back up file
set nobackup
" don't make swap file
set noswapfile

" title
set title

" 不可視文字を不可視化
set nolist
" 自動折り返ししない
set textwidth=0
" 長い行を@にさせない
set display=lastline
" 最低でも上下に表示する行数
set scrolloff=5
" 入力したコマンドを画面下に表示
set showcmd

" display status line
set laststatus=2
set statusline=[.%F%r%h%w%m]%=
set statusline+=%{'['.(&fenc!=''?&fenc:&enc).']['.&fileformat.']'}
set ruler " add cursor line location in right of status line

" disable conceal for tex
let g:tex_conceal='abdmg'
autocmd MyAutoCmd FileType latex :setlocal foldmethod=marker

" colorscheme
colorscheme iceberg
syntax enable

autocmd MyAutoCmd FileType help call s:set_japanese_document_format()
function! s:set_japanese_document_format()
        " change important keyword the last of lines ' >' and the top of lines '<'
        if &buftype != 'help'
                hi ignore ctermfg=red
                syntax match Error /\%>79v.*/
                syntax match Error /、\s/
                syntax match Error /。\s/
                set cc=+1
                let JpCountOverChars = 1
                set formatexpr=jpfmt#formatexpr()
                if has('conceal')
                        setlocal conceallevel=0
                endif
        endif
endfunction

set nohlsearch
if v:version > 800
        set nrformats=alpha,hex,bin
endif
" }}}

" ### Diff ### {{{
set diffexpr=MyDiff()
function! MyDiff()
        let opt = ""
        if &diffopt =~ "icase"
                let opt = opt . "-i "
        endif
        if &diffopt =~ "iwhile"
                let opt = opt . "-b "
        endif
        silent execute "!diff -a --binary " . opt . v:fname_in . " " . v:fname_new . " > " . v:fname_out
endfunction
" }}}

" ### completion ### {{{
" 入力補完機能を有効化
set wildmenu wildmode=longest:full
set completeopt=menu,preview

" spelling補完 on <C-x><C-s>
set spell
set spelllang+=cjk " 日本語をスペルチェックの対象 から除外する
" When press Ctrl + L while am typing, the previous spelling mistake is
" corrected.
inoremap <C-l> <c-g>u<Esc>[s1z=`]a<c-g>u

" tag Complete For html and xml file
augroup xml-html-comp
    autocmd!
    autocmd FileType html,xml inoremap <buffer> </ </<C-x><C-o>
augroup END

" dictionary Complete
augroup dict-comp
    autocmd!
    autocmd FileType javascript :setlocal dictionary=$HOME/.vim/dict/javascript.dict
    autocmd FileType html :setlocal dictionary=$HOME/.vim/dict/html.dict
    autocmd FileType css :setlocal dictionary=$HOME/.vim/dict/css.dict
    autocmd FileType latex :setlocal dictionary=$HOME/.vim/dict/tex.dict
    autocmd FileType python :setlocal dictionary=$HOME/.vim/dict/python.dict
    autocmd FileType cpp :setlocal dictionary=$HOME/.vim/dict/cpp.dict
    autocmd FileType c :setlocal dictionary=$HOME/.vim/dict/clang.dict
    autocmd FileType ruby :setlocal dictionary=$HOME/.vim/dict/ruby.dict
    autocmd FileType php :setlocal dictionary=$HOME/.vim/dict/php.dict
augroup END
" }}}

" ### Command ### {{{
" the count of command history
set history=2000
" いろんなコマンドの後にカーソルを先頭に移動させない
set nostartofline

" ### For Proxy Setting ### {{{
" Setting File For Proxy
" For example
"
" {
"   name: {
"     http: http://proxy.aa.bb:8080,
"     https: https://proxy.aa.bb:8080
"   }
" }
"
" git などのproxyを設定する。
command! -nargs=1 ProxySet call s:set_proxy(<f-args>)
command! ProxyUnset call s:unset_proxy()
let s:readfile=expand('~/.vim_proxy_setting.json')
if filereadable(s:readfile)
    " string型としてファイルを読み込む
    let s:lines=join(readfile(s:readfile, '', 10))
    " json形式へ変換
    let s:proxy_json=json_decode(s:lines)

    function! s:set_proxy(name) abort
        if has_key(s:proxy_json, a:name)
            if has_key(s:proxy_json[a:name], 'http')
                let s:http_proxy=s:proxy_json[a:name]["http"]
                call setenv("http_proxy", s:http_proxy)
                call system("git config --global http.proxy " . s:http_proxy )
                call system("npm config -g set proxy " . s:http_proxy )
                " Set ssh proxy
                " ~/.ssh/configの設定に書き込む
            endif

            if has_key(s:proxy_json[a:name], 'https')
                let s:https_proxy=s:proxy_json[a:name]["https"]
                call setenv("https_proxy", s:https_proxy)
                call system("git config --global https.proxy " . s:https_proxy )
                call system("npm config -g set https-proxy " . s:https_proxy )
                " Set ssh proxy
                " ~/.ssh/configの設定に書き込む
            endif
        endif
    endfunction

    function! s:unset_proxy() abort
        " unset system proxy setting
        call setenv("http_proxy", v:null)
        call setenv("https_proxy", v:null)

        " unset git global proxy setting
        call system("git config --global --unset http.proxy")
        call system("git config --global --unset https.proxy")
    endfunction
else
    echomsg "Not found '~/.vim_proxy_setting.json'"
endif
" }}}
" }}}
" }}}
" }}}

" ### key mapping ### {{{
" Wait key time
set timeout ttimeoutlen=50
" change Leader key to Space key
let mapleader = "\<Space>"
cnoremap <C-n> <Down>
cnoremap <C-p> <Up>
nnoremap ZQ <Nop>
" when tap :wq, make not save and close if buffer is no difference before
cnoremap wq x
" }}}

" ### plugin ### {{{
" vim-plug
" automated install vim-plug
if !filereadable(expand('$HOME/.vim/vim-plug/plug.vim'))
    let s:vim_plug_url='https://github.com/junegunn/vim-plug'
    call system("git clone " . s:vim_plug_url . " " . $HOME . "/.vim/vim-plug/")
endif
source ~/.vim/vim-plug/plug.vim

call plug#begin('~/.vim/plugged')
" For Completion and Snippet {{{
Plug 'Shougo/neosnippet.vim'
Plug 'Shougo/neosnippet-snippets'
if v:version > 704
    " Enable snipMate compatibility feature.
    let g:neosnippet#enable_snipmate_compatibility = 1
    " Tell Neosnippet about the My Snippets Directory
    let g:neosnippet#snippets_directory=['~/.vim/snippets/']
    " set key map for snippet
    imap <C-k> <plug>(neosnippet_expand_or_jump)
    smap <C-k> <plug>(neosnippet_expand_or_jump)
    xmap <C-k> <plug>(neosnippet_expand_target)
    smap <expr><TAB> neosnippet#expandable_or_jumpable() ?
                \ "\<Plug>(neosnippet_expand_or_jump)" : "\<TAB>"
    if has('conceal')
        set conceallevel=1 concealcursor=niv
    endif
endif
" }}}
Plug 'mattn/emmet-vim'

Plug 'mattn/sonictemplate-vim'
" Tell My Template Directory
let g:sonictemplate_vim_template_dir = [ '~/.vim/template' ]
let g:sonictemplate_vim_vars = {
            \ '_': {
            \   'author': 'Taichi Watanabe',
            \ },
            \}

" For Code Check
Plug 'w0rp/ale' " {{{
" code checker plugin
if has('job') && has('channel') && has('timers')
    let g:ale_sign_column_always = 1
    let g:ale_list_window_size = 2
    " format message
    let g:ale_echo_msg_error_str = 'E'
    let g:ale_echo_msg_warning_str = 'W'
    let g:ale_echo_msg_format = '[%linter%] %s [%severity%]'
    " move error mapping
    nmap <silent> <C-k> <plug>(ale_next_wrap)
    " check at :w
    let g:ale_lint_on_save = 1
    let g:ale_lint_on_text_changed = 'never'
    " Disable check at file open
    let g:ale_lint_on_enter = 0
    " use QuickFix instead of location list
    let g:ale_set_loclist = 0
    let g:ale_set_quickfix = 1

    " for npm local installed eslint and textlint
    if filereadable(expand('$PWD/.textlintrc')) ||
                \ filereadable(expand('$PWD/.eslintrc.json'))
        let $PATH .= ':' . $PWD . 'node_modules/.bin'
    endif

    let g:ale_cpp_gcc_options="-std=c++17 -Wall"
    let g:ale_c_gcc_options="-std=c99 -Wall"
    " set linter
    let g:ale_linters = {
                \ 'markdown' : ['textlint'],
                \ 'javascript' : ['eslint'],
                \ 'html' : ['cHTMLHint'],
                \ 'css' : ['csslint'],
                \ 'latex' : ['chktex'],
                \ 'c' : ['gcc'],
                \ 'cpp' : ['gcc'],
                \ 'java': [],
                \ }
    " set fixer
    nmap <space>= :ALEFix<CR>
endif
" }}}

Plug 'majutsushi/tagbar'
let g:tagbar_width = 30

" Language Server Protocol
Plug 'prabirshrestha/async.vim'
Plug 'prabirshrestha/vim-lsp'
" syntastic check disabled by lsp
let g:lsp_diagnostics_enabled = 0
let g:lsp_use_event_queue = 1

Plug 'fatih/vim-go', { 'for' : 'go', 'do': ':GoInstallBinaries' }
let g:go_fmt_command = "goimports"
let g:go_highlight_fields = 1
let g:go_highlight_functions = 1
" leave under functions to LSP
let g:go_def_mapping_enabled = 0
let g:go_doc_keywordprg_enabled = 0
let g:go_def_mode = 'gopls'
let g:go_info_mode = 'gopls'

if executable('go-langserver')
    augroup LspGo
        autocmd!
        autocmd User lsp_setup call lsp#register_server({
                    \ 'name': 'go-lang',
                    \ 'cmd': {server_info->['gopls']},
                    \ 'rootPatterns': ["go.mod", ".git/"],
                    \ 'whitelist': ['go'],
                    \ })
        autocmd FileType go setlocal omnifunc=lsp#complete
    augroup END
endif

" For javascript
Plug 'ryanolsonx/vim-lsp-javascript', { 'for' : 'js' }
if executable('typescript-language-server')
    " use directory with .git as root
    augroup LspJSTS
        autocmd!
        autocmd User lsp_setup call lsp#register_server({
                    \ 'name': 'javascript support using typescript-language-server',
                    \ 'cmd': { server_info->[&shell, &shellcmdflag, 'typescript-language-server --stdio']},
                    \ 'root_uri' : { server_info->lsp#utils#path_to_uri(lsp#utils#find_nearest_parent_directory(lsp#utils#get_buffer_path(), '.git/..'))},
                    \ 'whitelist' : ['javascript', 'javascript.jsx']
                    \ })
    augroup END
endif

if executable('solagraph')
    " gem install solagraph
    augroup LspRuby
        autocmd!
        autocmd User lsp_setup call lsp#register_server({
                    \ 'name': 'solagraph',
                    \ 'cmd': {server_info->[&shell, &shellcmdflag, 'solagraph stdio']},
                    \ 'initialization_options': { "diagnostics": "true"},
                    \ 'whitelist': ['ruby'],
                    \ })
    augroup END
endif
autocmd MyAutoCmd FileType ruby :setlocal isk+=@-@


if executable('css-languageserver')
    " npm install -g vscode-css-languageserver-bin
    augroup LspCSS
        autocmd!
        autocmd User lsp_setup call lsp#register_server({
                    \ 'name': 'css-languageserver',
                    \ 'cmd': {server_info->[&shell, &shellcmdflag, 'css-languageserver --stdio']},
                    \ 'whitelist': ['css', 'less', 'sass'],
                    \ })
    augroup END
endif

" npm install -g intelephense-server
" npm list -g | head
autocmd User lsp_setup call lsp#register_server({
            \ 'name': 'intelephense',
            \ 'cmd': {server_info->['node', expand('~/.nvm/versions/node/v11.7.0/lib/node_modules/intelephense-server/lib/server.js'), '--stdio']},
            \ 'whitelist': ['php'],
            \ })

" pip install python-language-server
Plug 'ryanolsonx/vim-lsp-python'
if executable('pyls')
    au User lsp_setup call lsp#register_server({
        \ 'name': 'pyls',
        \ 'cmd': {server_info->['pyls']},
        \ 'whitelist': ['python'],
        \ })
endif


" For LaTeX
Plug 'lervag/vimtex'
let g:tex_flavor='latex'
let g:vimtex_view_method='zathura'
let g:vimtex_quickfix_mode=0
let g:vimtex_compiler_latexmk = {
            \ 'background': 1,
            \ 'build_dir': '',
            \ 'continuous': 1,
            \ 'options': [
            \     '-pdfdvi',
            \     '-verbose',
            \     '-file-line-error',
            \     '-synctex=1',
            \     '-interaction=nonstopmode',
            \ ],
            \}

" For Clang
Plug 'homedm/cinclude.vim', { 'for': 'c' }
Plug 'justmao945/vim-clang'

" For writing
Plug 'andymass/vim-matchup'
set showmatch

" Runner
Plug 'thinca/vim-quickrun' " {{{
nnoremap <expr><silent> <space>c quickrun#is_runnting() ? quickrun#sweep_sessions() "\<C-c>"
" <Leader>qでquickrunで開かれた出力バッファを閉じる
nnoremap <Leader>q :<C-u>bw! \[QuickRun\ Output\]<CR>
let g:quickrun_config = {
            \ '_' : {
            \   'outputter/buffer/split' : ':botright 8sp',
            \   'outputter/buffer/close_on_empty' : 1,
            \   'outputter/buffer/name' : '[QuickRun Output]',
            \ },
            \ 'c/gcc' : {
            \   'cmdopt' : '-stc=c99 -Wall'
            \ },
            \ 'tex' : {
            \   'command' : 'platex',
            \   'exec' : ['%c -synctex=1 -interaction=nonstopmode %s', 'zathura %s:r.pdf']
            \ },
            \ }
" }}}

" ColorScheme
Plug 'cocopon/iceberg.vim', {'do': 'cp colors/* ~/.vim/colors/'}
Plug 'altercation/solarized', {'do': 'cp vim-colors-solarized/colors/* ~/.vim/colors/'}
Plug 'itchyny/lightline.vim'
let g:lightline = {
            \ 'colorscheme': 'solarized',
            \ }

" For CSV
Plug 'luochen1990/rainbow'
let g:rainbow_active=1 "0 if you want to enable it later via :RainbowToggle
Plug 'mechatroner/rainbow_csv'

" markdown
Plug 'previm/previm'
" open with Firefox
let g:previm_open_cmd = 'chromium'

" For Format
Plug 'vim-scripts/autodate.vim',
let autodate_format="%d/%m/%Y\ -\ %H:%M:%S"
Plug 'fuenor/JpFormat.vim', " {{{
" gqコマンドを実行前に自動整形をオフにする
nnoremap <silent> <expr> gq JpFormat_cmd("gq")
" .txt, .mdで自動整形を有効にする。
autocmd MyAutoCmd FileType markdown call s:set_autoformat()
autocmd MyAutoCmd FileType text call s:set_autoformat()
function! s:set_autoformat()
    let JpCountChars=80
    let JpCountOverChars=1
    set formatexpr=jpfmt#formatexpr()
endfunction
" }}}

Plug 'vim-scripts/Align'
" Setting for japanese environment
let g:Align_xstrlen = 3

Plug 'homedm/vim-better-whitespace'

" For Plugin
Plug 'mopp/layoutplugin.vim'

Plug 'vim-jp/vimdoc-ja'

"Draw image
Plug 'thinca/vim-painter'

Plug 'homedm/vim-play-piano'

Plug 'glidenote/memolist.vim'
let g:memolist_path = expand('$HOME/Documents/notebox/memo')
let g:memolist_memo_suffix = "md"
let g:memolist_memo_date = "%Y-%m-%d %H:%M"
call plug#end()
packadd termdebug
" }}}

" should write last line {{{
filetype plugin indent on
syntax on
set secure
" }}}
